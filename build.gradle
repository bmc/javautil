// NOTE: To publish: ./gradlew bintrayPublish

buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }

  dependencies {
      classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

plugins {
  id "com.jfrog.bintray" version "1.6"
}

allprojects {
  repositories {
    mavenLocal()
    jcenter()
  }

  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'java'
  apply plugin: 'com.jfrog.bintray'
}

import java.io.FileReader

defaultTasks 'build'

Properties projectProps = new Properties()
projectProps.load(new FileReader('src/main/resources/org/clapper/util/misc/Bundle.properties'))

def projectVersion = {
  projectProps.getProperty('api.version')
}

String userHome = System.getProperties().getProperty("user.home")
Properties bintrayProps = new Properties()
File bintrayCredentials = new File(userHome + "/.bintray/.credentials")
if (bintrayCredentials.exists()) {
  bintrayProps.load(new FileReader(bintrayCredentials))
}

String gnupgDir = userHome + "/.gnupg/"
String signingKeyFile = gnupgDir + "/signing-key"
String mavenGroupID = "org.clapper"

group = mavenGroupID
version = projectVersion()

publishing {
  publications {
    JavaUtil(MavenPublication) {
      from components.java
      groupId mavenGroupID
      artifactId 'javautil'
      version projectVersion()
    }
  }
}

bintray {
  user = bintrayProps.getProperty("user")
  key = bintrayProps.getProperty("password")

  pkg {
    repo = 'maven'
    name = 'javautil'
    licenses = ['BSD']
    vcsUrl = 'https://github.com/bmc/javautil'
    labels = ['javautil']
    publications = ['JavaUtil']
    websiteUrl = 'http://software.clapper.org/javautil'
    issueTrackerUrl = 'https://github.com/bmc/javautil/issues'
    githubRepo = 'bmc/javautil'
    githubReleaseNotesFile = 'README.md'

    version {
      name = projectVersion()
      desc = "JavaUtil utility library ${projectVersion()}"
      released  = new Date()
      vcsTag = "release-${projectVersion()}"
      gpg {
        sign = true
      }
      //attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
    }
  }
}

project.ext {
  author = "Brian M. Clapper"
  version = projectVersion
}

dependencies {
  compile group: 'javax.activation', name: 'activation', version: '1.1'
  compile group: 'javax.mail', name: 'mail', version: '1.4+'
  compile group: 'asm', name: 'asm', version: '3.3+'
  compile group: 'asm', name: 'asm-commons', version: '3.3+'
  compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1+'
  compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.6.4'

  testCompile group: 'junit', name: 'junit', version: '4+'
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}

